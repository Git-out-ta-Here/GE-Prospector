plugins {
    id 'java'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.runelite.net'
    }
}

def runeLiteVersion = '1.10.51'

dependencies {
    compileOnly group: 'net.runelite', name: 'client', version: runeLiteVersion
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    
    // HTTP client for API calls
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    
    // JSON processing
    implementation 'com.google.code.gson:gson:2.8.9'
    
    // Guava for caching
    implementation 'com.google.guava:guava:31.1-jre'
    
    // Charts
    implementation 'org.jfree:jfreechart:1.5.4'
    
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'net.runelite', name: 'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name: 'jshell', version: runeLiteVersion
    testImplementation 'org.mockito:mockito-core:4.5.1'
}

group = 'com.geprospect '
version = '1.0.0'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(11)
}

tasks.register('shadowJar', Jar) {
    dependsOn configurations.testRuntimeClasspath
    manifest {
        attributes('Main-Class': 'com.example.ExamplePluginTest', 'Multi-Release': true)
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.output
    from sourceSets.test.output
    from {
        configurations.testRuntimeClasspath.collect { file ->
            file.isDirectory() ? file : zipTree(file)
        }
    }

    exclude 'META-INF/INDEX.LIST'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude '**/module-info.class'

    group = BasePlugin.BUILD_GROUP
    archiveClassifier.set('shadow')
    archiveFileName.set("${rootProject.name}-${project.version}-all.jar")
}
